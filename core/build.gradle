plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'jvm-test-suite'
    id 'java-test-fixtures'
    alias(libs.plugins.springBootPlugin)
    alias(libs.plugins.springDepManagementPlugin)
    alias(libs.plugins.kotlinJvm)
}

group = 'by.mrrockka'
version = '1.2.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(22)
    }
}

kotlin {
    jvmToolchain(22)
}

defaultTasks "clean", "assemble"

tasks.withType(JavaCompile) {
    options.compilerArgs.addAll([
            '--enable-preview',
            '-Amapstruct.suppressGeneratorTimestamp=true',
            '-Amapstruct.defaultComponentModel=spring',
    ])
    options.release.set(22)
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation libs.springBootJdbc
    implementation libs.postgresql
    implementation libs.liquibase
    implementation libs.jacksonDataformat
    implementation libs.mapstructCore
    implementation libs.lombokMapstructBinding
    implementation libs.hikariCP
    implementation libs.kotlinStdLib

    compileOnly libs.lombok
    annotationProcessor libs.lombok
    annotationProcessor libs.mapstructProcessor

    testCompileOnly libs.lombok
    testAnnotationProcessor libs.lombok
    testAnnotationProcessor libs.mapstructProcessor
    testImplementation libs.mockitoCore
    testImplementation libs.mockitoJupiter
    testImplementation libs.assertjCore
    testImplementation libs.kotlinStdLib

    testFixturesCompileOnly libs.lombok
    testFixturesAnnotationProcessor libs.lombok
    testFixturesAnnotationProcessor libs.mapstructProcessor
    testFixturesApi libs.springBootJdbc
    testFixturesApi libs.springBootTest
    testFixturesApi libs.testContainerPostgresql
    testFixturesApi libs.testContainerJupiter
    testFixturesApi libs.jupiter
    testFixturesApi(libs.javaFaker) {
        exclude group: 'org.yaml'
    }
}

testing {
    suites {
        configureEach {
            useJUnitJupiter()
            targets {
                all {
                    testTask.configure {
                        jvmArgs('--enable-preview')
                        testLogging {
                            events("passed", "failed")
                        }
                    }
                }
            }
        }

        integrationTest(JvmTestSuite) {
            dependencies {
                implementation libs.assertjCore
                implementation libs.springBootTest

                compileOnly libs.lombok
                annotationProcessor libs.lombok
                annotationProcessor libs.mapstructProcessor

                implementation(project())
                implementation(testFixtures(project()))
            }
        }
    }
}

bootJar {
    enabled = false
}

bootRun {
    enabled = false
}

jar {
    enabled = true
}